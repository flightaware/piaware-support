#!/usr/bin/env tclsh

#
# generate dump1090 and beast-splitter configuration
#

package require fa_config_generator

proc generate_receiver_config {config} {
	generate_beast_splitter_config $config
	generate_dump1090_config $config
	generate_dump978_config $config
	return 1
}

proc generate_beast_splitter_config {config} {
	if {![$config get allow-modeac]} {
		# disable A/C on the input side
		set force "--force j"
	} else {
		set force ""
	}

	switch -nocase [$config get receiver-type] {
		beast {
			lappend relayconfig "ENABLED=yes"

			set baud [$config get beast-baudrate]
			if {$baud ne ""} {
				lappend relayconfig "INPUT_OPTIONS=\"--serial /dev/beast --fixed-baud $baud $force\""
			} else {
				lappend relayconfig "INPUT_OPTIONS=\"--serial /dev/beast $force\""
			}
		}

		relay {
			lappend relayconfig "ENABLED=yes"
			lappend relayconfig "INPUT_OPTIONS=\"--net [$config get receiver-host]:[$config get receiver-port] $force\""
		}

		radarcape {
			lappend relayconfig "ENABLED=yes"
			lappend relayconfig "INPUT_OPTIONS=\"--net [$config get radarcape-host]:10003 $force\""
		}

		default {
			lappend relayconfig "# beast-splitter does not run when receiver-type=[$config get receiver-type]"
			lappend relayconfig "ENABLED=no"
		}
	}

	lappend relayconfig "OUTPUT_OPTIONS=\"--listen 30005:R --connect localhost:30104:R\""
	::fa_config_generator::generate_file "/etc/default/beast-splitter" $relayconfig
	return 1
}

proc generate_dump1090_config {config} {
	set receiverOpts [list]
	set enabled 1

	switch -nocase [$config get receiver-type] {
		rtlsdr {
			lappend receiverOpts "--net-bo-port 30005" "--fix"
			lappend receiverOpts "--device-index" [$config get rtlsdr-device-index]
			set gain [$config get rtlsdr-gain]
			if {$gain eq "auto"} {
				lappend receiverOpts "--gain" -10
			} else {
				lappend receiverOpts "--gain" $gain
			}
			lappend receiverOpts "--ppm" [$config get rtlsdr-ppm]
		}

		bladerf {
			lappend receiverOpts "--net-bo-port 30005" "--fix"
			lappend receiverOpts "--device-type" "bladerf"
			lappend receiverOpts "--gain" "66"
			lappend receiverOpts "--bladerf-fpga" "/usr/share/dump1090-fa/bladerf/decimate8-x40.rbf"
			lappend receiverOpts "--bladerf-decimation" "8"
			lappend receiverOpts "--bladerf-bandwidth" "14000000"
			lappend receiverOpts "--freq" "1085200000"
		}

		beast - radarcape - relay {
			lappend receiverOpts "--net-only" "--net-bo-port 0" "--fix"
		}

		other {
			set enabled 0
		}

		default {
			::fa_config_generator::logger "unrecognized receiver-type '[$config get receiver-type]', not configuring dump1090"
			set enabled 0
		}
	}

	if {![$config get allow-modeac]} {
		lappend receiverOpts "--no-modeac-auto"
	}

	set dump1090config [list]
	if {$enabled} {
		lappend dump1090config "ENABLED=yes"
		lappend dump1090config {DECODER_OPTIONS="--max-range 360"}
		lappend dump1090config {NET_OPTIONS="--net --net-heartbeat 60 --net-ro-size 1000 --net-ro-interval 1 --net-http-port 0 --net-ri-port 0 --net-ro-port 30002 --net-sbs-port 30003 --net-bi-port 30004,30104"}
		lappend dump1090config {JSON_OPTIONS="--json-location-accuracy 2"}
		lappend dump1090config "RECEIVER_OPTIONS=\"[join $receiverOpts " "]\""
	} else {
		lappend dump1090config "# dump1090-fa does not run when receiver-type=[$config get receiver-type]"
		lappend dump1090config "ENABLED=no"
	}

	::fa_config_generator::generate_file "/etc/default/dump1090-fa" $dump1090config
	return 1
}

proc generate_dump978_config {config} {
	set receiverOpts [list]
	set enabled 1

	switch -nocase [$config get uat-receiver-type] {
		sdr {
			set gain [$config get uat-sdr-gain]
			if {$gain eq "auto"} {
				lappend receiverOpts "--sdr-auto-gain"
			} else {
				lappend receiverOpts "--sdr-gain" $gain
			}
			set ppm [$config get uat-sdr-ppm]
			if {$ppm ne 0} {
				lappend receiverOpts "--sdr-ppm" $ppm
			}
			lappend receiverOpts "--format CS8"
			lappend receiverOpts "--sdr" [$config get uat-sdr-device]
			lappend receiverOpts "--raw-port 30978"
			lappend receiverOpts "--json-port 30979"
		}

		none {
			set enabled 0
		}

		default {
			::fa_config_generator::logger "unrecognized uat-receiver-type '[$config get uat-receiver-type]', not configuring dump978"
			set enabled 0
		}
	}

	set dump1090config [list]
	if {$enabled} {
		lappend dump978config "ENABLED=yes"
		lappend dump978config "RECEIVER_OPTIONS=\"[join $receiverOpts " "]\""
	} else {
		lappend dump978config "# dump978-fa does not run when uat-receiver-type=[$config get uat-receiver-type]"
		lappend dump978config "ENABLED=no"
	}

	::fa_config_generator::generate_file "/etc/default/dump978-fa" $dump978config
	return 1
}

if {!$tcl_interactive} {
	::fa_config_generator::generate_from_argv generate_receiver_config $argv
}
