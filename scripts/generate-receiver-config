#!/usr/bin/env tclsh

#
# generate dump1090 and beast-splitter configuration
#

package require fa_config_generator

proc generate_receiver_config {config} {
	generate_beast_splitter_config $config
	generate_dump1090_config $config
	return 1
}

proc generate_beast_splitter_config {config} {
	switch -nocase [$config get receiver-type] {
		beast {
			lappend relayconfig "ENABLED=yes"
			lappend relayconfig "INPUT_OPTIONS=\"--serial /dev/beast\""
		}

		relay {
			lappend relayconfig "ENABLED=yes"
			lappend relayconfig "INPUT_OPTIONS=\"--net [$config get receiver-host]:[$config get receiver-port]\""
		}

		radarcape {
			lappend relayconfig "ENABLED=yes"
			lappend relayconfig "INPUT_OPTIONS=\"--net [$config get radarcape-host]:10003\""
		}

		default {
			lappend relayconfig "# beast-splitter does not run when receiver-type=[$config get receiver-type]"
			lappend relayconfig "ENABLED=no"
		}
	}

	lappend relayconfig "OUTPUT_OPTIONS=\"--listen 30005:R --connect localhost:30104:R\""
	::fa_config_generator::generate_file "/etc/default/beast-splitter" $relayconfig
	return 1
}

proc generate_dump1090_config {config} {
	set dump1090config {
		{DECODER_OPTIONS="--max-range 360"}
		{NET_OPTIONS="--net --net-heartbeat 60 --net-ro-size 1000 --net-ro-interval 1 --net-http-port 0 --net-ri-port 0 --net-ro-port 30002 --net-sbs-port 30003 --net-bi-port 30004,30104"}
		{JSON_OPTIONS="--json-location-accuracy 2"}
	}

	switch -nocase [$config get receiver-type] {
		rtlsdr {
			set index [$config get rtlsdr-device-index]
			set gain [$config get rtlsdr-gain]
			set ppm [$config get rtlsdr-ppm]
			lappend dump1090config "RECEIVER_OPTIONS=\"--device-index $index --gain $gain --ppm $ppm --net-bo-port 30005 --fix\""
		}

		beast - radarcape - relay - other {
			lappend dump1090config "RECEIVER_OPTIONS=\"--net-only --net-bo-port 0\""
		}

		default {
			::fa_config_generator::logger "unrecognized receiver type '[$config get receiver-type]', not configuring dump1090"
			# no config
			return 0
		}
	}

	::fa_config_generator::generate_file "/etc/default/dump1090-fa" $dump1090config
	return 1
}

if {!$tcl_interactive} {
	::fa_config_generator::generate_from_argv generate_receiver_config $argv
}
